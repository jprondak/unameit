#! /bin/sh
#
# $Id: $
#
# Copyright (c) 1997 Enterprise Systems Management Corp.
#
# This file is part of UName*It.
#
# UName*It is free software; you can redistribute it and/or modify it under
# the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 2, or (at your option) any later
# version.
#
# UName*It is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
# for more details.
#
# You should have received a copy of the GNU General Public License
# along with UName*It; see the file COPYING.  If not, write to the Free
# Software Foundation, 59 Temple Place - Suite 330, Boston, MA
# 02111-1307, USA.
#

# reexec with tcl\
exec tcl "$0" "$@"

if {[info level]} return

proc tcl2c_load_procs {} {
    global oldprocs argv name
    foreach p [info procs] {
	set oldprocs($p) {}
    }
    foreach f $argv {
	source $f
    }
}

set argv [lassign $argv name]
tcl2c_load_procs

#
# Replace with C "\\n", end the string and continue on next line.
#
set nlsub {\\n"}
append nlsub "\n"
append nlsub {    "}

puts "static char *$name\[\]={"
foreach p [info procs] {
    if {[info exists oldprocs($p)]} continue
    puts -nonewline {    "proc }
    set alist {}
    foreach a [info args $p] {
	if {[info default $p $a def]} {
	    lappend alist [list $a $def]
	} else {
	    lappend alist $a
	}
    }
    puts -nonewline "[list $p] [list $alist] {"
    set y [info body $p]
    # 
    # Worry about '\', which is special to C
    #
    regsub -all {\\} $y {\\\\} x
    #
    # Similarly, we need to hide any " chars in the strings
    #
    regsub -all {\"} $x {\\"} y
    #
    # Replace new lines with C newlines
    #
    regsub -all "\n" $y $nlsub x
    puts -nonewline $x
    puts "}\","
}

puts " (char *)0};"
